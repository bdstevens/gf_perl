#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long;
use GFProject;
use Helpers;
use LexcoFile;
use MMSFile;

## Provide usage information
my $usage_text =
"Usage: $0 accepts these parameters:\n" .
"  (opt) '-i' will interactively request all parameters\n" .
"  (req) GeoFrame project name\n" .
"  (req) GeoFrame project password\n" .
"  (req) Name of Lexco well export file (well.txt)\n" .
"  (req) Name of MMS file (DSPTSFfd.txt)\n" .
"  (req) Name of output file for MMS deviations\n";
my $ask;
GetOptions("i" => \$ask);
unless ($#ARGV == 3 or $ask) { print STDERR $usage_text; exit 1 }

## If we're interactive, ask for everything; otherwise, grab what we're given
my ($proj_name, $proj_pass, $lexco_file, $mms_file, $output_file);
if ($ask) {
	$proj_name = prompt_with "Enter name of GeoFrame project to query: ";
	$proj_pass = prompt_with "Enter password of GeoFrame project to query: ";
	$lexco_file = prompt_with "Enter name of Lexco well export file: ";
	$mms_file = prompt_with "Enter name of MMS file: ";
	$output_file = prompt_with "Enter name of output file for MMS deviations: "
} else {
	($proj_name, $proj_pass, $lexco_file, $mms_file) = @ARGV
}

## Connect to the project
my $proj = GFProject->connect($proj_name, $proj_pass) or die;

## Create a list of UWI's which have no "real" deviation survey
my %uwis;
for ($proj->getUwisWithoutRealDeviations) {
	if (length $_ == 10) { 
	  $uwis{$_ . "0000"} = $_
	} elsif (length $_ == 12) {
	  $uwis{$_ . "00"} = $_
	} else {
		$uwis{$_} = $_
	};
}

## Open lexco_file for reading
my $lexco = LexcoFile->read($lexco_file);

## Load all of the deviation records in mms_file
my $mms = MMSFile->Load($mms_file);

## Open the output file if we're interactive
if ($ask) { open OUTPUT, ">$output_file" or die "Could not open file \" $output_file\"!\n" }

## Print any directional surveys for requested UWI's
while (my $lexco_record = $lexco->nextRecord) {
	next unless $uwis{$lexco_record->getUwi};
	if (my $mms_record = $mms->findByUwi($lexco_record->getUwi)) {
		my $long = $lexco_record->getUwi;
		my $short = $uwis{$long};
		if (($_ = $mms_record->getDirectionalSurvey) =~ s/$long/$short/mg) { $ask ? print OUTPUT : print }
	}
}
