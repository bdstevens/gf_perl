#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long;
use GFProject;
use Helpers;
use PI297Record;
use PI297File;
use POSIX qw/strftime/;

## Provide for conversion between PIDwights' well status codes and GeoFrame's 
my %well_codes = (
	0 => 26,
	1 => 48,
	2 => 30,
	3 => 30,
	4 => 48,
	5 => 30,
	6 => 30,
	7 => 30,
	8 => 17,
	9 => -1,
	A => 27,
	L => 3,
	S => 16,
	T => 1
);

## Provide usage information
my $usage_text =
"Usage: $0 accepts these parameters:\n" .
"  (opt) '-i' will interactively request all parameters\n" .
"  (req) GeoFrame project name\n" .
"  (req) GeoFrame project password\n" .
"  (req) Name of PI Dwights export file (*.97f)\n" .
"  (req if interactive) Name of output file\n";
my $ask;
GetOptions("i" => \$ask);
unless ($#ARGV == 2 or $ask) { print STDERR $usage_text; exit 1 }

## If we're interactive, ask for everything; otherwise, grab what we're given
my ($proj_name, $proj_pass, $pi_file, $output_file);
if ($ask) {
	$proj_name = prompt_with "Enter name of GeoFrame project to query: ";
	$proj_pass = prompt_with "Enter password of GeoFrame project to query: ";
	$pi_file = prompt_with "Enter name of PIDwights export file (*.97f): ";
	$output_file = prompt_with "Enter name of output file to create: "
} else {
	($proj_name, $proj_pass, $pi_file) = @ARGV;
}


## Connect to the project
my $proj = GFProject->connect($proj_name, $proj_pass) or die;

## Get a list of all UWI's currently in the project
my %uwis_in_project;
map { $uwis_in_project{$_} = 1 } $proj->getAllUwis;

## Open the output file if we're interactive
if ($ask) { open OUTPUT, ">$output_file" or die }

## Process the file
my $input = PI297File->read($pi_file);
my $included = my $total = 0;
my ($record, $date, $day, $month, $year);
while ($record = $input->nextRecord) {
	$total++;
	## If this UWI already exists in the project
	## or if a comparable 12 digit UWI already exists in the project
	## or if a comparable 10 digit UWI already exists in the project
	## skip it
	next if (
		$uwis_in_project{$record->getUwi} or
		$uwis_in_project{substr($record->getUwi, 0, 12)} or
		$uwis_in_project{substr($record->getUwi, 0, 10)});
	
	## Some entries seem to be missing location info; skip them
	unless ($record->getLongitude && $record->getLatitude) {
		print STDERR "Warning: discarding ", $record->getUwi, " as it has no location information\n";
		next
	}
	## Convert the date to a GeoFrame friendly format
	if ($record->getSpudDate) {
		$day = substr($record->getSpudDate, 6, 2);
		$month = substr($record->getSpudDate, 4, 2);
		$year = substr($record->getSpudDate, 0, 4);
		$date = POSIX::strftime("%d-%b-%Y", 0, 0, 0, $day, $month - 1, $year - 1900)
	} else {
		$date = ''
	}
	## Create the output
	$included++;
	$_ = join("|", $record->getUwi, $record->getWellName, $record->getLeaseName, $record->getWellNumber, $record->getLatitude, $record->getLongitude, $record->getOperator, $date, $record->getElevation, $record->getMeasuredDepth, $well_codes{$record->getFinalWellClass}) . "\n";
	$ask ? print OUTPUT : print
}

print STDERR "Processed $total wells\n";
print STDERR "Included $included wells\n";
